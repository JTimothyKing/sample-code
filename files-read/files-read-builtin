#!/usr/bin/env perl
use strict;
use warnings;

use Benchmark ':hireswallclock';

use File::Find ();
use File::Spec ();

my $mark_start = Benchmark->new();

my $perl_prefix_path = do {
    my $path = $^X;
    my $bindir = '/bin/';
    $path =~ s|$bindir ((?!$bindir).)* $||x
        or die "cannot determine Perl install prefix\n";
    $path;
};
print STDERR "Scanning Perl prefix path $perl_prefix_path\n";

my @all_files;

my $total_file_size = 0;
File::Find::find({
    no_chdir => 1,
    wanted => sub {
        my $file = $File::Find::name;
        my $filesize = -s $file;
        if ($filesize) {
            $total_file_size += $filesize;
            push @all_files, $file;
        }
    },
}, $perl_prefix_path);
print STDERR "$total_file_size bytes to read\n";

my $mark_got_files = Benchmark->new();

my $bytes_read = 0;
FILE: for my $file (@all_files) {
    open my $fh, '<', $file
        or warn("cannot open file '$file'\n"), next FILE;
    BLOCK: while (1) {
        my $data;
        read($fh, $data, 32768) or last BLOCK;
        $bytes_read += length $data;
    }
}
print STDERR "$bytes_read bytes read\n";

my $mark_end = Benchmark->new();

print ' Scan time: ', timestr(timediff($mark_got_files, $mark_start)), "\n",
      ' Read time: ', timestr(timediff($mark_end, $mark_got_files)), "\n",
      'Total time: ', timestr(timediff($mark_end, $mark_start)), "\n";

__END__
